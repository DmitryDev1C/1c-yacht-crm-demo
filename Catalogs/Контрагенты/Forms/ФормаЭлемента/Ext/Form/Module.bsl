
&НаКлиенте
Процедура СсылкаНаКартинкуНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Фильтр = "Все картинки (*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf)|*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf|"
	+ "Формат bmp (*.bmp;*.dib;*.rle)|*.bmp;*.dib;*.rle|"
	+ "Формат JPEG (*.jpg;*.jpeg)|*.jpg;*.jpeg|"
	+ "Формат TIFF (*.tif)|*.tif|"
	+ "Формат GIF (*.gif)|*.gif|"
	+ "Формат PNG (*.png)|*.png|"
	+ "Формат icon (*.ico)|*.ico|"
	+ "Формат метафайл (*.wmf;*.emf)|*.wmf;*.emf|";
	
	Диалог = Новый ПараметрыДиалогаПомещенияФайлов("Выберите файл", Ложь, Фильтр);
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияДиалогаВыбораФайла", ЭтаФорма);
	НачатьПомещениеФайлаНаСервер(Оповещение, , , , Диалог, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияДиалогаВыбораФайла (ОписаниеФайла, ДопПараметры) Экспорт;
	Если ОписаниеФайла = Неопределено Тогда
		Возврат		
	КонецЕсли;	
	Модифицированность = Истина;
	СсылкаНаДокумент = ОписаниеФайла.Адрес;  
КонецПроцедуры


&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЭтоАдресВременногоХранилища(СсылкаНаДокумент) Тогда
		ТекущийОбъект.ФотоДокумента = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(СсылкаНаДокумент));
	ИначеЕсли СсылкаНаДокумент = "" Тогда
		ТекущийОбъект.ФотоДокумента = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СсылкаНаДокумент = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ФотоДокумента");
КонецПроцедуры

&НаСервере
Процедура УдалитьКартинкуНаСервере()
	СсылкаНаДокумент = "";
КонецПроцедуры

&НаКлиенте
Процедура УдалитьКартинку(Команда)
	УдалитьКартинкуНаСервере();
КонецПроцедуры
